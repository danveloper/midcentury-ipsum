buildscript {
  repositories {
    jcenter()
    maven { url "http://oss.jfrog.org/repo" }
    maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.19"
    classpath "com.github.jengelman.gradle.plugins:shadow:1.2.2"
    classpath "se.transmode.gradle:gradle-docker:1.2"
    classpath "com.github.robfletcher:compass-gradle-plugin:2.0.6"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:0.12.1230"
  }
}

repositories {
  jcenter()
  maven { url "http://oss.jfrog.org/repo" }
  maven { url "http://repo.spring.io/repo" }
}

apply plugin: "kotlin"
apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "docker"
apply plugin: "com.github.robfletcher.compass"
apply plugin: "idea"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

mainClassName = "com.energizedwork.midcenturyipsum.MidcenturyipsumPackage"

compass {
  cssDir = file("src/ratpack/public/css")
  sassDir = file("src/main/sass")
  imagesDir = file("src/ratpack/public/img")
  relativeAssets = true
  debugInfo = false
  environment = "production"
}

processResources.inputs.files compassCompile
run.dependsOn compassWatchStart
run.finalizedBy compassWatchStop
clean.dependsOn cleanCompassCompile

idea {
  project {
    jdkName "1.8"
    languageLevel "1.8"
  }
}

dependencies {
  springloaded "org.springframework:springloaded:1.2.3.RELEASE"
  compile "org.jetbrains.kotlin:kotlin-stdlib:0.12.1230"
  testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

run {
  systemProperty "ratpack.reloadable", "true"
}

task wrapper(type: Wrapper) {
  gradleVersion = "2.6"
}

installApp {
  from run.workingDir
}

startScripts {
  doLast {
    unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dratpack.port=$PORT"')
  }
}

task stage(dependsOn: ["clean", "installApp"], description: "Heroku uses this as default")

task midcenturyIpsumDocker(type: Docker) {
  dependsOn shadowJar
  applicationName = "midcentury-ipsum"
  tagVersion = "0.1"
  dockerfile = project.file("Dockerfile")

  doFirst {
    addFile project.tasks.shadowJar.archivePath
  }
}

build.dependsOn midcenturyIpsumDocker